set terminal svg background 'white'

reset
f(x,y)=(1-x)*(1-x) + 100*(y-x*x)*(y-x*x)
set xrange [-5:5]
set yrange [-5:5]
set isosample 250, 250
set table 'gpiData.txt'
splot f(x,y)
unset table

set contour base
set cntrparam level incremental 0, 10000, 100000
unset surface
set table 'contours.txt'
splot f(x,y)
unset table

reset
set xrange [-5:5]
set yrange [-5:5]
set palette rgbformulae 33,13,10
set key top right
set xlabel 'x'
set ylabel 'y'
set title 'Path of nmsimplex2 over Rosenbrock function'
p 'gpiData.txt' with image\
 ,'contours.txt' w l lt -1 lw 1.5 notitle\
 ,'rosen.txt' u 2:3 w l lw 2 lc 'red' title 'Algorithm path'\
 ,'< tail -n 1 rosen.txt' u 2:3 with point pointtype 2 title 'Minimum'
