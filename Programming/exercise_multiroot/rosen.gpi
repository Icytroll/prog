set terminal svg background 'white'

reset
f(x,y)=(1-x)*(1-x) + 100*(y-x*x)*(y-x*x)
set xrange [-2:5]
set yrange [-2:10]
set isosample 250, 250
set table 'data.txt'
splot f(x,y)
unset table

set contour base
set cntrparam level incremental 0, 5000, 100000
unset surface
set table 'cont.txt'
splot f(x,y)
unset table

reset
set xrange [-2:5]
set yrange [-2:10]
set palette rgbformulae 33,13,10
set key bottom left
set xlabel 'x'
set ylabel 'y'
set title 'Path of root-finding algorithm over Rosenbrock function'
p 'data.txt' with image\
 ,'cont.txt' w l lt -1 lw 1.5 notitle\
 ,'rosen.txt' u 1:2 w l lw 2 lc 'red' title 'Algorithm path'\
 ,'< tail -n 1 rosen.txt' u 1:2 with point pointtype 2 title 'Minimum'
